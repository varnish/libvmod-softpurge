varnishtest "Test softpurge vmod"

server s1 {
	rxreq
	txresp

	accept
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
import softpurge from "${vmod_topbuild}/src/.libs/libvmod_softpurge.so";
	backend b1 {
		.host = "${s1_addr}";
		.port = "${s1_port}";
	}

	sub vcl_recv {
		set req.backend_hint = b1;    
		if (req.method == "PURGE") { return(hash); } 
		if (req.method == "SOFTPURGE") { return(hash); } 
	}

	sub vcl_backend_response {
		set beresp.ttl = 1s;
		set beresp.grace = 1s;
	}

	sub vcl_miss {
		set req.http.x-via = "miss";
	}

	sub vcl_hit {
		set req.http.x-via = "hit";
		if (req.method == "SOFTPURGE") {
			set req.http.x-via = "hit";
			set req.http.msg = "Softpurged";
			softpurge.softpurge();
		}
		if (req.method == "PURGE") {
			set req.http.msg = "Purged";
		}    
	}

	sub vcl_deliver {
		set resp.http.x-object-hits = obj.hits;
		set resp.http.x-via = req.http.x-via;
		set resp.http.msg = req.http.msg;
	}

} -start

client c1 {
	txreq -url "/"
	rxresp

	txreq -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.x-via == "hit"

	# run the softpurge
	txreq -req "SOFTPURGE" -url "/"
	rxresp
} -run

client c2 {
	# check that it was a miss + successful fetch.
	txreq -url "/"
	rxresp
	expect resp.http.x-via == "hit"
	expect resp.status == 200
	expect resp.http.x-object-hits != 0

	txreq -req "PURGE" -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.msg == "Purged"
} -run

delay 3

varnish v1 -expect n_object == 0
